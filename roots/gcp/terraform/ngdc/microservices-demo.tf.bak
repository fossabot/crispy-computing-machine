variable "gcp_project_id" {
  type        = string
  description = "The GCP project ID to apply this config to"
}

variable "name" {
  type        = string
  description = "Name given to the new GKE cluster"
  default     = "pauls-boutique"

}

variable "region" {
  type        = string
  description = "Region of the new GKE cluster"
  default     = "us-central1"

}

variable "namespace" {
  type        = string
  description = "Kubernetes Namespace in which the Online Boutique resources are to be deployed"
  default     = "default"

}

variable "filepath_manifest" {
  type        = string
  description = "Path to Online Boutique's Kubernetes resources, written using Kustomize"
  default     = "../kustomize/"

}

variable "memorystore" {
  type        = bool
  description = "If true, Online Boutique's in-cluster Redis cache will be replaced with a Google Cloud Memorystore Redis cache"
}

locals {
  base_apis = [
    "container.googleapis.com",
    "monitoring.googleapis.com",
    "cloudtrace.googleapis.com",
    "cloudprofiler.googleapis.com"

  ]
  memorystore_apis = ["redis.googleapis.com"]
  #cluster_name     = google_container_cluster.my_cluster.name

}

module "enable_google_apis" {
  source                      = "terraform-google-modules/project-factory/google//modules/project_services"
  version                     = "~> 14.0"
  project_id                  = var.gcp_project_id
  disable_services_on_destroy = true
  activate_apis               = concat(local.base_apis, var.memorystore ? local.memorystore_apis : [])
}

resource "google_container_cluster" "cluster" {
  name             = var.name
  location         = var.region
  enable_autopilot = true
  ip_allocation_policy {}
  deletion_protection = false
  depends_on          = [module.enable_google_apis]
}

output "cluster_location" {
  description = "Location of the cluster"
  value       = resource.google_container_cluster.cluster.location
}

output "cluster_name" {
  description = "Name of the cluster"
  value       = resource.google_container_cluster.cluster.name
}
